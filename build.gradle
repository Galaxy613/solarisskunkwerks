apply plugin: 'java'

allprojects {
    version '0.7.5'
    ext {
        releaseType='Nightly'
        date=getDate()
    }
    jar {
        onlyIf { !sourceSets.main.allSource.files.isEmpty() }
    }
}

subprojects {
    group 'com.solarisskunkwerks'
    repositories {
        mavenCentral()
    }
}

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate.toString()
}

evaluationDependsOnChildren()

task copyDocs(dependsOn: subprojects.build, type: Copy) {
    from 'Docs'
    into "${buildDir}/release/SSW_${version}/Docs"
}

task copyData(dependsOn: subprojects.build, type: Copy) {
    from 'Data'
    into "${buildDir}/release/SSW_${version}/Data"
}

task copyDeps(dependsOn: subprojects.build, type: Copy) {
    from subprojects.collect { it.configurations.runtime }
    into "$buildDir/release/SSW_${version}/lib"
}

task copyJars(dependsOn: subprojects.build, type: Copy) {
    from subprojects.collect { it.tasks.withType(Jar) }
    exclude "**/sswlib.jar"
    into "$buildDir/release/SSW_${version}"
}

task releaseBuild(dependsOn: [copyDocs, copyData, copyDeps, copyJars]) { }

task zipRelease(type: Zip, dependsOn: releaseBuild) {
    from "${buildDir}/release"
        include "**/"
    if ("$releaseType" == "Nightly") {
        archiveName "SSW_${version}.${date}.zip"
    } else {
        archiveName "SSW_${version}.zip"
    }
}